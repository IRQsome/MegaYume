CON
'{
_CLKFREQ = 300_000_000
HYPER_ACCESSORY = 0 ' Base pin for P2EVAL HyperRAM board
HYPER_CLK    =  8+HYPER_ACCESSORY
HYPER_RWDS   = 10+HYPER_ACCESSORY
HYPER_SELECT = 12+HYPER_ACCESSORY
HYPER_BASE   =  0+HYPER_ACCESSORY
HYPER_RESET  = 15+HYPER_ACCESSORY
'}

{
_CLKFREQ = 10_000_000
'}

HYPER_WAIT  = 28
HYPER_DELAY = HYPER_WAIT*2+1

DAT
        org
        asmclk



        fltl #HYPER_CLK
        wrpin ##P_TRANSITION|P_OE, #HYPER_CLK
        wxpin #2, #HYPER_CLK
        drvl #HYPER_CLK

        wrpin ##P_INVERT_OUTPUT,#HYPER_SELECT
        drvl #HYPER_SELECT

        drvh #HYPER_RESET

        setxfrq nco_slow

        waitx #200

        debug("init ok")
        debug(uhex_long(#@romio_area))


        mov iter,#5
.lp
        wrlong #"A",##@romio_area
        mov pa,##$100 >> 2
        mov read_longs,#4
        call #readburst
        rdlong tmp1,##@romio_area
        debug(uhex_long(tmp1),lstr(#@romio_area,#16),uhex_byte_array(#@romio_area,#16))
        djnz iter,#.lp

        jmp #$

readburst
              mov tmp1,#(6+HYPER_WAIT)
              shl read_longs,#3
              add tmp1,read_longs
              fltl #HYPER_CLK
              rczr pa wcz
              rczl tmp2
              and tmp2,#%11
              shl tmp2,#9
              setbyte pa,#%101_00000,#3 ' read linear burst
              movbyts pa, #%%0123
              drvh #HYPER_SELECT
              drvl bus_pinfield
              drvl #HYPER_CLK
              xinit addr_cmd1,pa
              wypin tmp1,#HYPER_CLK
              xcont addr_cmd2,tmp2
              setq nco_fast
              xcont #HYPER_DELAY,#0
              wrfast bit31,read_dest
              shr read_longs,#1
              setword read_cmd,read_longs,#0
              waitxmt
              fltl bus_pinfield
              setq nco_slow
              xcont read_cmd,#0
              waitxfi
       _ret_  drvl #HYPER_SELECT


bit31         long  negx
bus_pinfield  long HYPER_BASE addpins 7
addr_cmd1     long (HYPER_BASE<<17)|X_PINS_ON | X_IMM_4X8_1DAC8 + 4
addr_cmd2     long (HYPER_BASE<<17)|X_PINS_ON | X_IMM_4X8_1DAC8 + 2
read_cmd      long (HYPER_BASE<<17)|X_WRITE_ON| X_8P_1DAC8_WFBYTE

nco_fast      long $8000_0000
nco_slow      long $4000_0000

read_longs    long 4
read_dest     long @romio_area

tmp1          res 1
tmp2          res 1
iter          res 1


DAT
              orgh
              alignl
romio_area    byte 0[256]

